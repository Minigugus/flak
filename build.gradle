plugins {
  id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'eclipse'

def PUBLISH_PROJECTS = [
  'flak-api',
  'flak-spi',
  'flak-backend-jdk',
  'flak-jackson',
  'flak-login',
  'flak-resource',
  'flak-util'
]

allprojects {
  apply plugin: 'java'
  apply plugin: 'ivy-publish'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  version = "${version_flak}" // see gradle.properties

  repositories {
    mavenCentral()
  }

  dependencies {
    testCompile "junit:junit:4.11"
  }

  if (PUBLISH_PROJECTS.contains(project.name)) {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'
    apply plugin: "ivy-publish"

    bintray {
      user = System.getenv('BINTRAY_USER')
      key = System.getenv('BINTRAY_KEY')
      publications = ['MyPublication']
      publish = true

      pkg {
        repo = 'maven'
        name = project.name
        userOrg = System.getenv('BINTRAY_USER')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pcdv/flak.git'
        websiteUrl = 'https://github.com/pcdv/flak'
        issueTrackerUrl = 'https://github.com/pcdv/flak/issues'
        description = project.description
        version {
          name = "${version_flak}"
          desc = project.description
          released = new Date()
        }
      }
    }


    publishing {
      repositories {
        ivy {
          name "local"
          url "${System.properties['user.home']}/.ivy2/local"
        }
      }
      publications {
        // allows to do a local publish
        ivy(IvyPublication) {
          organisation "com.github.pcdv.flak"
          from components.java
        }
        MyPublication(MavenPublication) {
          from components.java
          groupId 'com.github.pcdv.flak'
          artifactId project.name
          version "${version}"

          artifact sourceJar {
            classifier "sources"
          }
        }
      }
    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
        'Implementation-Version': project.version
    }
  }
}
