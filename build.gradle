def PUBLISH_PROJECTS = [
  'flak-api',
  'flak-spi',
  'flak-backend-jdk',
  'flak-jackson',
  'flak-login',
  'flak-resource',
  'flak-swagger',
  'flak-util'
]

allprojects {
  apply plugin: 'java'
  apply plugin: 'ivy-publish'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
  }

  dependencies {
    testImplementation "junit:junit:4.11"
  }

  if (PUBLISH_PROJECTS.contains(project.name)) {
    apply plugin: 'java-library'
    apply plugin: "ivy-publish"
    apply plugin: "maven-publish"

    publishing {
      repositories {
        ivy {
          name "local"
          url "${System.properties['user.home']}/.ivy2/local"
        }
      }
      publications {
        // allows to do a local publish
        ivy(IvyPublication) {
          organisation "com.github.pcdv.flak"
          from components.java
        }

        // publishToMavenLocal (or jitpack.io) using -Pversion=x.y.z
        maven(MavenPublication) {
          groupId = 'com.github.pcdv.flak'
          from components.java
        }
      }
    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
        'Implementation-Version': project.version
    }
  }
}
